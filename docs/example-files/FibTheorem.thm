Kind nat type.
Type z nat.
Type s nat -> nat.

Define nat: nat -> prop by
; nat z
; nat (s X) := nat X.

Define lt: nat -> nat -> prop by
; lt z (s X)
; lt (s X) (s Y) := lt X Y.

Define leq: nat -> nat -> prop by
; leq z X
; leq (s X) (s Y) := leq X Y.

Define plus: nat -> nat -> nat -> prop by
; plus z X X
; plus (s X) Y (s Z) := plus X Y Z.

Define times: nat -> nat -> nat -> prop by
; times z X z
; times (s X) Y Z := exists U, times X Y U /\ plus U Y Z.

Define fib: nat -> nat -> prop by
; fib z z
; fib (s z) (s z)
; fib (s (s X)) K :=
    exists M N, fib X M /\ fib (s X) N /\ plus M N K.

Theorem plus_deterministic: forall x y u v, plus x y u -> plus x y v -> u = v.
induction on 1. intros. case H1.
  case H2. search.
  case H2. apply IH to *H3 *H4. search.

Theorem times_deterministic: forall x y u v, times x y u -> times x y v -> u = v.
induction on 1. intros. case H1.
  case H2. search.
  case H2. apply IH to *H3 *H5. apply plus_deterministic to *H4 *H6. search.

Theorem fib_deterministic: forall x y u, fib x y -> fib x u -> y = u.
induction on 1. intros. case H1.
  case H2. search.
  case H2. search.
  case H2.
    apply IH to *H3 *H6.
    apply IH to *H4 *H7.
    apply plus_deterministic to *H5 *H8. search.

Theorem lt_irreflexive: forall x, nat x -> lt x x -> false.
induction on 1. intros. case H1.
  case H2.
  case H2. apply IH to *H3 *H4.

Theorem plus_result_nat: forall x y k, nat x -> nat y -> plus x y k -> nat k.
induction on 3. intros. case H3.
  search.
  case H1. apply IH to *H5 *H2 *H4. search.

Theorem times_result_nat: forall x y k, nat x -> nat y -> times x y k -> nat k.
induction on 3. intros. case H3.
  search.
  case H1. apply IH to *H6 H2 *H4. apply plus_result_nat to *H7 *H2 *H5. search.

Import "damf:bafyreibrzv44jglcptuuiiol3spglqr5i2ttadwg2tvueyjedbpf5v3p5a" as
Theorem fib_square_above: forall x, nat x ->
  leq (s^13 z) x ->
  forall y, times x x y ->
  forall u, fib x u -> lt y u.

Import "damf:bafyreigw52p4flvo7wnp4bl7kdkkqjo5opn6xv6g3onhj5obzjheqbvoki" as
Theorem sq1: times (s^1 z) (s^1 z) (s^1 z).

Import "damf:bafyreic6sswb47edfafdwkp6i6nhx64dp4exalvxkodzv4vupwxlrsykxa" as
Theorem sq2: times (s^2 z) (s^2 z) (s^4 z).

Import "damf:bafyreiadawlvj3272a6xmouks2bmxhlxtxuunfpdrgsoqiw7gyhlmaealu" as
Theorem sq3: times (s^3 z) (s^3 z) (s^9 z).

Import "damf:bafyreig2raf6r3rcv4wktejcwsyk7lpzixcxrhmkafsw4gxkb6tpeoz7ve" as
Theorem sq4: times (s^4 z) (s^4 z) (s^16 z).

Import "damf:bafyreidtm2iwx57w3ym464gmorbevswlwuqeinenfwi475grylzwbmgnj4" as
Theorem sq5: times (s^5 z) (s^5 z) (s^25 z).

Import "damf:bafyreihk7wrmsnh3rrbwuc36xpdzam5t7sp2cbegn4xopws4t6v7zzacmy" as
Theorem sq6: times (s^6 z) (s^6 z) (s^36 z).

Import "damf:bafyreiffos2a3aurvv62h6hvwthhzvxr6ljpikqvooeli6qhfj5x2o5kau" as
Theorem sq7: times (s^7 z) (s^7 z) (s^49 z).

Import "damf:bafyreifxjknfmxtxfevdpa2sb5niuieozskorks572q6ci5mwdltheq4iu" as
Theorem sq8: times (s^8 z) (s^8 z) (s^64 z).

Import "damf:bafyreih7k5n5eupjusle7e4flnmwwfugdmjabgfxhlbvdfogog3zpj4nge" as
Theorem sq9: times (s^9 z) (s^9 z) (s^81 z).

Import "damf:bafyreia3u5ilcqgmknoxmyenmbcg6zs5dpn4gb4mgekvayg7sapuyczpba" as
Theorem sq10: times (s^10 z) (s^10 z) (s^100 z).

Import "damf:bafyreihh3u5odbn7b6mg7gj5kbddgx24vmzsk3s7fl4pbwpt3vt5tozgum" as
Theorem sq11: times (s^11 z) (s^11 z) (s^121 z).

Import "damf:bafyreidowzhpu4awe2ikqcvezgvt5alayzuggjdjfv2cml5lvjdqgb5hgm" as
Theorem sq12: times (s^12 z) (s^12 z) (s^144 z).

Import "damf:bafyreiah3ozon7snoeoda4d7k6jomkxxpc7zkdbqdpmuunswmcnl4ti6hi" as
Theorem sq13: times (s^13 z) (s^13 z) (s^169 z).

Import "damf:bafyreihgsvb4j6yqywfpglosxiqf5lestlunnt4dofh7hu7o57ysc3prqa" as
Theorem fib2: fib (s^2 z) (s^1 z).

Import "damf:bafyreiarvy76rs6dhno2bb2td65afej65qcffwzvv6yma6adck7jart5ta" as
Theorem fib3: fib (s^3 z) (s^2 z).

Import "damf:bafyreiajt7jdw5edowkckjgzx6oywcu2nqlkvwj44jwfutyid47236riaq" as
Theorem fib4: fib (s^4 z) (s^3 z).

Import "damf:bafyreifudehu2yowq7inyl76xbe4mh373bkdxxjx5x7xyeo6kttqdu7gii" as
Theorem fib5: fib (s^5 z) (s^5 z).

Import "damf:bafyreia3r2yuaf7mlzev23mbrnbhqpv4mt7poy6rutnluixbrhkziodyrm" as
Theorem fib6: fib (s^6 z) (s^8 z).

Import "damf:bafyreibuyhtzhqiat4xxfcl2ih2ody5vlib4az3vtlktpfymm6iday6zu4" as
Theorem fib7: fib (s^7 z) (s^13 z).

Import "damf:bafyreia33kwqu63c4u7d5aigobablwcluebfto6xovlthiy4jqwwkswlyi" as
Theorem fib8: fib (s^8 z) (s^21 z).

Import "damf:bafyreid5ktnm355nffj7sgmdcgclsnfxuuoqedyzexapzcns3gvvpaut5a" as
Theorem fib9: fib (s^9 z) (s^34 z).

Import "damf:bafyreigqjhcvo2vrhlntnx5gbiirbq3qjszpgj5mjixu2ycgzf7v3kskfy" as
Theorem fib10: fib (s^10 z) (s^55 z).

Import "damf:bafyreifmla2g6h4nwp226jl34i5zopiwcjyooq2t2z3vo26axfnlls7mdq" as
Theorem fib11: fib (s^11 z) (s^89 z).

Import "damf:bafyreiejcs3d2cyfg32thgza3gadx73po5ajnzbkckxhsooj2k22o3q5ry" as
Theorem fib12: fib (s^12 z) (s^144 z).

Import "damf:bafyreigwivd6ymclghz3oav3uvgs4avadyhxyswbj7w364v2c5olzfsxje" as
Theorem fib13: fib (s^13 z) (s^233 z).

Theorem fib_squares: forall x x2, nat x -> times x x x2 ->
  (fib x x2 -> x = z \/ x = s z \/ x = s^12 z) /\
  (x = z \/ x = s z \/ x = s^12 z -> fib x x2).
intros Hnat Hsquare. split.
%% ->
intros Hfib.
Hcs: assert x = s^0 z \/ x = s^1 z \/ x = s^2 z \/ x = s^3 z
         \/ x = s^4 z \/ x = s^5 z \/ x = s^6 z \/ x = s^7 z
         \/ x = s^8 z \/ x = s^9 z \/ x = s^10 z \/ x = s^11 z
         \/ x = s^12 z \/ leq (s^13 z) x.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  Hnat: case Hnat. search.
  right. search 14.
Hcs: case Hcs.
  search. % case of x = z
  search. % case of x = s z
  % case of x = 2
  apply fib_deterministic to fib2 Hfib.
  apply times_deterministic to sq2 Hsquare.
  % case of x = 3
  apply fib_deterministic to fib3 Hfib.
  apply times_deterministic to sq3 Hsquare.
  % case of x = 4
  apply fib_deterministic to fib4 Hfib.
  apply times_deterministic to sq4 Hsquare.
  % case of x = 5
  apply fib_deterministic to fib5 Hfib.
  apply times_deterministic to sq5 Hsquare.
  % case of x = 6
  apply fib_deterministic to fib6 Hfib.
  apply times_deterministic to sq6 Hsquare.
  % case of x = 7
  apply fib_deterministic to fib7 Hfib.
  apply times_deterministic to sq7 Hsquare.
  % case of x = 8
  apply fib_deterministic to fib8 Hfib.
  apply times_deterministic to sq8 Hsquare.
  % case of x = 9
  apply fib_deterministic to fib9 Hfib.
  apply times_deterministic to sq9 Hsquare.
  % case of x = 10
  apply fib_deterministic to fib10 Hfib.
  apply times_deterministic to sq10 Hsquare.
  % case of x = 11
  apply fib_deterministic to fib11 Hfib.
  apply times_deterministic to sq11 Hsquare.
  % case of x = 12
  search.
  % case of x >= 13
  H: apply fib_square_above to Hnat Hcs.
  H: apply *H to Hsquare.
  H: apply *H to Hfib.
  Hnat2: apply times_result_nat to Hnat Hnat Hsquare.
  apply lt_irreflexive to Hnat2 H.

%% <-
intros Hcs.
Hcs: case Hcs.
  case Hsquare. search.
  apply times_deterministic to sq1 Hsquare. search.
  apply times_deterministic to sq12 Hsquare. backchain fib12.